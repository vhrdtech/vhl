token_stream = _{ SOI ~ "r#\"" ~ token_tree* ~ "\"#" ~ EOI }

token_tree = _{ token | delim_token_tree }
token = { interpolate | interpolate_repetition | ident | punctuation | literal | delimiter }
delim_token_tree = { "(" ~ token_tree* ~ ")" | "[" ~ token_tree* ~ "]" | "{" ~ token_tree* ~ "}" }

interpolate = { interpolate_simple | interpolate_path }
interpolate_simple = _{ "#" ~ interpolate_path_segment }
interpolate_path_segment = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")* }
interpolate_path = _{ "#{" ~ interpolate_path_segment ~ ("." ~ interpolate_path_segment)* ~ "}" }

repetition_separator = { !"*" ~ punctuation | &("*" ~ "*") ~ punctuation }
interpolate_repetition = {
    "#" ~ "(" ~
    (!"#" ~ token_tree)* ~
    interpolate ~
    (!"#" ~ token_tree)* ~
    interpolate? ~
    (!"#" ~ token_tree)* ~
    ")" ~ repetition_separator? ~ "*"
}

ident = @{ XID_START ~ XID_CONTINUE* }

punct_eqeq = _{ "==" }
punct_assign = _{ "=" }

punct_shl = _{ "<<" }
punct_rhl = _{ ">>" }

punct_le = _{ "<=" }
punct_lt = _{ "<" }
punct_ne = _{ "!=" }
punct_ge = _{ ">=" }
punct_gt = _{ ">" }

punct_andand = _{ "&&" }
punct_oror = _{ "||" }

punct_double_question = _{ "??" }
punct_question_dot = _ { "?." }
punct_excl_dot = _ { "!." }
punct_impl = _ { "->" }

punct_tilde = _{ "~" }
punct_excl = _{ "!" }

punct_triple_slash = _{ "///" }
punct_double_slash = _{ "//" }
punct_slash = _{ "/" }

punct_plus = _{ "+" }
punct_minus = _{ "-" }
punct_star = _{ "*" }

punct_percent = _{ "%" }

punct_caret = _{ "^" }
punct_single_quote = _{ "'" }
punct_and = _{ "&" }
punct_or = _{ "|" }

punct_at = _{ "@" }
punct_dotdot = _{ ".." }
punct_dot = _{ "." }
punct_comma = _{ "," }
punct_semicolon = _{ ";" }
punct_double_colon = _{ "::" }
punct_colon = _{ ":" }
punct_rarrow = _{ ">" }
punct_larrow = _{ "<" }
punct_pound = _{ "#" }
punct_dollar = _{ "$" }
punct_question = _{ "?" }

punct_newline = _{ "\\n" }

punctuation = {
    punct_eqeq |
    punct_assign |
    punct_shl |
    punct_rhl |
    punct_le |
    punct_lt |
    punct_ne |
    punct_ge |
    punct_gt |
    punct_andand |
    punct_oror |
    punct_double_question |
    punct_question_dot |
    punct_excl_dot |
    punct_impl |
    punct_tilde |
    punct_excl |
    punct_triple_slash |
    punct_double_slash |
    punct_slash |
    punct_plus |
    punct_minus |
    punct_star |
    punct_percent |
    punct_caret |
    punct_single_quote |
    punct_and |
    punct_or |
    punct_at |
    punct_dotdot |
    punct_dot |
    punct_comma |
    punct_semicolon |
    punct_double_colon |
    punct_colon |
    punct_rarrow |
    punct_larrow |
    punct_pound |
    punct_dollar |
    punct_question |
    punct_newline
}

delimiter = { "\\\\(" | "\\\\)" | "\\\\{" | "\\\\}" | "\\\\[" | "\\\\]" }

dec_lit_raw = @{ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }
literal = { dec_lit_raw }

//ds_comment = @{ !"///" ~ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
//ts_comment = @{ "///" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

WHITESPACE = _{ (" " | "\t" | "\r" | "\n")+ }